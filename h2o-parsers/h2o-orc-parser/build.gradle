//
// H2O Orc Parser
//
description = "H2O Orc Parser"

configurations{
    // Configuration used to get all transitive dependencies for org.apache.hadoop:hadoop-common
    // We do this to avoid manually excluding the libraries
    hadoopCommonExclude
    // The same for hive exec
    hiveExecExclude
}

dependencies {
  def hadoopVersion="2.8.4"
  def hiveExecVersion="1.1.0"
  hadoopCommonExclude("org.apache.hadoop:hadoop-common:$hadoopVersion")
  hiveExecExclude("org.apache.hive:hive-exec:$hiveExecVersion"){
      // this dependency need to be excluded manually as Gradle can't find it in maven central
      exclude group: 'org.pentaho', module: 'pentaho-aggdesigner-algorithm'
      exclude group: 'eigenbase', module: 'eigenbase-properties'
  }
  api project(":h2o-core")
  // Only PersistHDFS API
  api(project(":h2o-persist-hdfs")) {
      exclude group: 'ai.h2o', module: 'h2o-core'
      exclude group: 'net.java.dev.jets3t', module: 'jets3t'
      exclude group: 'org.apache.hadoop'
  }

  // Note: What is connection between hive-exec version and hadoop-version and orc version?
  // Note: In this case we are using hive version which is compatible with $defaultHadoopVersion
  // Note: for newest version it should be replaces by hive-orc
  api("org.apache.hive:hive-exec:$hiveExecVersion") {
      // we can't use transitive=false so we need to exclude the dependencies manually
      configurations.hiveExecExclude.getResolvedConfiguration().getResolvedArtifacts().each {
          if (it.moduleVersion.id.group != "org.apache.hive" && it.moduleVersion.id.module.name != "hive-exec") {
              exclude group: it.moduleVersion.id.group, module: it.moduleVersion.id.module.name
          }
      }
      exclude group: 'org.pentaho', module: 'pentaho-aggdesigner-algorithm'
  }
  // For compilation we need common
  api("org.apache.hadoop:hadoop-common:$hadoopVersion") {
      // we can't use transitive=false so we need to exclude the dependencies manually
      configurations.hadoopCommonExclude.getResolvedConfiguration().getResolvedArtifacts().each {
          if (it.moduleVersion.id.group != "org.apache.hadoop" && it.moduleVersion.id.module.name != "hadoop-common") {
              exclude group: it.moduleVersion.id.group, module: it.moduleVersion.id.module.name
          }
      }
  }

  testImplementation project(":h2o-test-support")
  testRuntimeOnly project(":${defaultWebserverModule}")
  // We need correct version of MapRe Hadoop to run JUnits
  testRuntimeOnly("org.apache.hadoop:hadoop-client:$hadoopVersion") {
      exclude module: "jasper-runtime"
      exclude module: "jasper-compiler"
      exclude module: "curator-client"
      exclude module: "curator-recipes"
      exclude module: "servlet-api"
      exclude module: "zookeper"
  }
}

apply from: "${rootDir}/gradle/dataCheck.gradle"

test {
  dependsOn ":h2o-core:testJar"
  dependsOn smalldataCheck, jar, testJar, testMultiNode

  // Defeat task 'test' by running no tests.
  exclude '**'
}
