#!/usr/bin/groovy

@Library('test-shared-library') _

def dockerImage

def setPrismaScanningStages(assemblyType, stageIndex) {
    branchName = "${env.BRANCH_NAME}".replace('/', '-')
    assemblyImage = "h2o-assemblies/${assemblyType}:${BUILD_NUMBER}-${branchName}"
    
    stage("${stageIndex}.A. Build image for ${assemblyType}") {
        script {
            sh "docker build . -t ${assemblyImage} -f ./docker/prisma/Dockerfile.${assemblyType}jars"
        }
    }
    stage ("${stageIndex}.B. Scan ${assemblyType} jar using Snyk") {
        withCredentials([string(credentialsId: 'H2O_3_SNYK_TOKEN_JENKINS_TEXT', variable: 'SNYK_TOKEN')]) {
            script {
                sh "./snyk container test ${assemblyImage} --file=./docker/prisma/Dockerfile.${assemblyType}jars --severity-threshold=medium --app-vulns --nested-jars-depth=4 | tee ${assemblyImage}-snyk.out || true"
            }
            archiveArtifacts artifacts: "${assemblyImage}-snyk.out"
        }
    }
    stage("${stageIndex}.C. Scan ${assemblyType} jar using Prisma") {
        script {
            // scan the image
            prismaCloudScanImage ca: '',
                    cert: '',
                    dockerAddress: 'unix:///var/run/docker.sock',
                    image: "${assemblyImage}",
                    key: '',
                    logLevel: 'info',
                    podmanPath: '',
                    project: '',
                    resultsFile: "prisma-${assemblyType}-scan-results.json",
                    ignoreImageBuildTime: true
        }
    }
    stage("${stageIndex}.D. Export results for ${assemblyType} jar to CSV") {
        withCredentials([usernamePassword(credentialsId: 'twistlock_credentials', usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
            sh "curl -k -u \$USERNAME:\$PASSWORD https://mr-0xz1:8083/api/v1/scans/download?search=${assemblyImage} > ${assemblyImage}-prisma.csv"
        }
        archiveArtifacts artifacts: "${assemblyImage}-prisma.csv"
    }
    stage("${stageIndex}.E. Publish report for ${assemblyType} jar") {
        prismaCloudPublish resultsFilePattern: "prisma-${assemblyType}-scan-results.json"
    }
}

pipeline {
    agent { node { label 'h2o-3' } }

    options {
        ansiColor('xterm')
        timestamps()
    }

    stages {
        stage('0. Init'){
            steps{
                script{
                    dir("docker/prisma"){
                        dockerImage = docker.build("node-java","-f Dockerfile .")
                    }
                    sh "curl --compressed https://static.snyk.io/cli/latest/snyk-linux -o snyk"
                    sh "chmod +x ./snyk"
                }
                
            }
        }
        stage('1. Build jars') {
            steps {
                script{
                    dockerImage.inside(){
                        sh "./gradlew :h2o-assemblies:steam:shadowJar"
                        sh "./gradlew :h2o-assemblies:main:shadowJar"
                        archiveArtifacts artifacts: "h2o-assemblies/steam/build/libs/*.jar"
                        archiveArtifacts artifacts: "h2o-assemblies/main/build/libs/*.jar"
                    }
                }
            }
        }
        stage('2. Steam assembly jar (Prisma)') {
            steps {
                setPrismaScanningStages("steam", 2)
            }
        }
        stage('3. Main assembly jar (Prisma)') {
            steps {
                setPrismaScanningStages("main", 3)
            }
        }
    }
    post {
      always {
        cleanWs()
      }
    }
}
