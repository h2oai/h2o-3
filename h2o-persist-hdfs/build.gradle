
description = "H2O Persist HDFS"

configurations {
    compile.exclude module: 'servlet-api'
    testCompile.exclude module: 'servlet-api'
}

dependencies {
    compile project(":h2o-core")
    compile('net.java.dev.jets3t:jets3t:0.9.0')
    compile("org.apache.hadoop:$defaultHdfsDependency:$defaultHadoopVersion") {
        // Pull all dependencies to allow run directly from IDE or command line
        transitive = true
    }
    compile("org.apache.hadoop:hadoop-aws:$defaultHadoopVersion")
    compile("com.google.guava:guava:24.1.1")

    testCompile project(":h2o-test-support")
    testRuntimeOnly project(":${defaultWebserverModule}")
}

apply from: "${rootDir}/gradle/dataCheck.gradle"

task s3CredentialsCheck(group: "S3") {
    description = "Validates that core-site.xml file is present."
    doLast {
        def coreSitePath = System.getenv("JUNIT_CORE_SITE_PATH")
        if (! coreSitePath)
            throw new ResourceException('Environment property JUNIT_CORE_SITE_PATH is not defined.')
        if (! file(coreSitePath).isFile())
            throw new ResourceException("File ${coreSitePath} does not exist")
    }
}

test {
    dependsOn ":h2o-core:testJar"
    dependsOn smalldataCheck, s3CredentialsCheck, jar, testJar, testMultiNode

    // Defeat task 'test' by running no tests.
    exclude '**'
}
