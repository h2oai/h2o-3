apply plugin: 'scala'

// Specify properties
ext {
  scalaBinaryVersion = '2.10'
  scalaVersion = '2.10.4'
}

// Activate Zinc compiler and configure scalac
tasks.withType(ScalaCompile) {
    scalaCompileOptions.useCompileDaemon = false
    scalaCompileOptions.useAnt = false
    scalaCompileOptions.additionalParameters = ['-target:jvm-1.6']
}

jar {
  baseName = "${project.name}_${scalaBinaryVersion}"
}

// Create jar
task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "${project.name}_${scalaBinaryVersion}"
    appendix = 'test'
    from sourceSets.test.output
}

// Create jar with scaladoc
task scaladocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'scaladoc'
    from scaladoc
}

// Create a configuration containing only for test artifacts
configurations {
    testArchives
}

// Explicitly
artifacts {
    testArchives testJar
    archives scaladocJar
}

// Enable support for multi-node testing
apply from: "$rootDir/gradle/multiNodeTesting.gradle"

