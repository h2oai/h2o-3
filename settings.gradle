import org.gradle.api.internal.DefaultDomainObjectSet

rootProject.name = 'h2o-3'

include 'h2o-core'
include 'h2o-algos'
include 'h2o-web'
include 'h2o-app'
include 'h2o-r'
include 'h2o-py'
include 'h2o-assemblies:main'
include 'h2o-assemblies:genmodel'
//include 'h2o-persist-gcs'
//include 'h2o-persist-hdfs'
//include 'h2o-persist-s3'
//include 'h2o-persist-http'
include 'h2o-docs'
include 'h2o-hadoop-common'
include 'h2o-mapreduce-generic'
include 'h2o-hive'
include 'h2o-genmodel'
include 'h2o-bindings'
include 'h2o-test-integ'
include 'h2o-test-accuracy'
//include 'h2o-avro-parser'
//include 'h2o-orc-parser'
//include 'h2o-parquet-parser'
//include 'h2o-parquet-v17-compat'
include 'h2o-jaas-pam'
include 'h2o-automl'
include 'h2o-genmodel-ext-xgboost'
include 'h2o-ext-xgboost'
//include 'h2o-ext-krbstandalone'
include 'h2o-webserver-iface'
include 'h2o-jetty-8'
include 'h2o-jetty-9'
include 'h2o-ext-jython-cfunc'
include 'h2o-ext-mojo-pipeline'
include 'h2o-ext-target-encoder'
include 'h2o-security'
include 'h2o-logger'
include 'h2o-genmodel-ext-jgrapht'
include 'h2o-k8s'
include 'h2o-genmodel-ext-deepwater'

// GRPC support
if ("true".equals(System.getenv("H2O_BUILD_GRPC"))) {
    include 'h2o-assemblies:py2o'
    include 'h2o-grpc'
}

// Reconfigure scala projects to support cross compilation
// The following code will create two projects for each included item:
//   For example: h2o-scala_2.10, h2o-scala_2.11

class ScalaCrossCompileExtension {

  private String defaultScalaVersion

  private boolean buildDefaultOnly = false

  def targetVersions = new DefaultDomainObjectSet(String)

  def projectsToCrossBuild = new DefaultDomainObjectSet(String)

  void targetVersions(String... versions) {
    targetVersions.addAll(versions as List)
  }


  void defaultScalaVersion(String defaultScalaVersion) {
    defaultScalaVersion = defaultScalaVersion
  }

  void buildDefaultOnly(boolean buildDefaultOnly) {
    buildDefaultOnly = buildDefaultOnly
  }

  String getDefaultScalaVersion() {
    return defaultScalaVersion
  }

  boolean getBuildDefaultOnly() {
    return buildDefaultOnly
  }

  void include(String projectName) {
    projectsToCrossBuild.add(projectName)
  }
}

class ScalaCrossBuildPlugin implements Plugin<Settings> {
  static getScalaVersionSuffix(String version) {
    return "_${getScalaBaseVersion(version)}"
  }

  /* Scala base version for Scala < 2.10 is the same as version number (minor releases are not
     binary compatible). For Scala >= 2.10 the major version represents compatibility group.
   */
  static getScalaBaseVersion(String version) {
    return version.startsWith("2.10.") ? "2.10"
            : version.startsWith("2.11") ? "2.11"
            : version
  }
  private static def includeSuffixedProject(settings, module, scalaVersion, mapping) {
    def path = module + getScalaVersionSuffix(scalaVersion)
    settings.include(path)

    def project = settings.findProject(path.startsWith(":") ? path : ":" + path)
    project.projectDir = new File(project.projectDir.parent, module.split(':').last())
    mapping.put(project.name, scalaVersion)
  }

  void apply(Settings settings) {

    def scalaCrossCompile = settings.extensions.create('scalaCrossCompile', ScalaCrossCompileExtension)//, settings.startParameter.projectProperties)
    def projectMapping = new HashMap<String, String>() // project name to version mapping

    scalaCrossCompile.projectsToCrossBuild.all { module ->
      if (scalaCrossCompile.buildDefaultOnly) {
        includeSuffixedProject(settings, module, scalaCrossCompile.defaultScalaVersion, projectMapping)
      } else {
        scalaCrossCompile.targetVersions.each { v ->
          includeSuffixedProject(settings, module, v, projectMapping)
        }
      }
    }
    // Modify the projects to produce right build directory and
    // propagate scala version
    // FIXME: introduce extension to simplify passing of scala version
    settings.gradle.projectsLoaded { g ->
      g.rootProject.subprojects { subproject ->
        // It is cross compiled project
        if (projectMapping.containsKey(subproject.name))  {
          def projectScalaVersion = projectMapping[subproject.name]
          subproject.ext.scalaBaseVersion = getScalaBaseVersion(projectScalaVersion)
          subproject.ext.scalaVersion = projectScalaVersion
          subproject.buildDir = "${subproject.buildDir}/${subproject.name}"
        }
      }
    }
  }
}

apply plugin: ScalaCrossBuildPlugin

scalaCrossCompile {
    targetVersions "2.10.7", "2.11.12"
    include 'h2o-scala'
}

// Make structure flat and avoid annoying dummy modules
rootProject.children.each { project ->
  if (project.name.startsWith("h2o-") && project.name.endsWith("-parser")) {
    String projectDirName = "h2o-parsers/${project.name}"
    project.projectDir = new File(settingsDir, projectDirName)
  }
  // Parser Compatibility Testing
  if (project.name.startsWith("h2o-") && project.name.endsWith("-compat")) {
    String projectDirName = "h2o-parsers/" + project.name.replaceFirst("-v[0-9]+-", "-") + "/" + project.name
    project.projectDir = new File(settingsDir, projectDirName)
  }
  if (project.name.startsWith("h2o-ext-")) {
    String projectDirName = "h2o-extensions/${project.name - 'h2o-ext-'}"
    project.projectDir = new File(settingsDir, projectDirName)
  }
  if (project.name.startsWith("h2o-genmodel-ext-")) {
    String projectDirName = "h2o-genmodel-extensions/${project.name - 'h2o-genmodel-ext-'}"
    project.projectDir = new File(settingsDir, projectDirName)
  }
}

//
// Include Hadoop builds only if requested
//
if (System.getProperty("user.name").equals("jenkins")
        || System.getenv("BUILD_HADOOP") != null
        || System.getenv("H2O_TARGET") != null) {

  // Default hadoop build targets
  def allTargets = [
         "cdh5.4", "cdh5.5", "cdh5.6", "cdh5.7", "cdh5.8", "cdh5.9", "cdh5.10", "cdh5.13", "cdh5.14", "cdh5.15", "cdh5.16", 
         "cdh6.0", "cdh6.1", "cdh6.2", "cdh6.3", "cdp7.0",
         "hdp2.2", "hdp2.3", "hdp2.4", "hdp2.5", "hdp2.6","hdp3.0","hdp3.1",
         "mapr4.0", "mapr5.0", "mapr5.1", "mapr5.2", "mapr6.0", "mapr6.1", "iop4.2"
  ]
  // Compute targets
  def targets = System.getenv("H2O_TARGET") != null ? System.getenv("H2O_TARGET").split(",").collect { it.trim() } : allTargets
  // Include selected/all Hadoop targets
  if (System.getenv("BUILD_HADOOP") != "false") {
    targets.each { name ->
      include "h2o-hadoop-2:h2o-${name}-assembly"
      include "h2o-hadoop-3:h2o-${name}-assembly"
    }
  }
}
