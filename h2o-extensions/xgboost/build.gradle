description = "H2O XGBoost support"

ext {
    xgboost4jVersion = "1.0.0.11"
}

dependencies {
    compile project(":h2o-core")
    compile project(":h2o-algos")
    compile project(":h2o-genmodel")
    compile project(":h2o-genmodel-ext-xgboost")
    compileOnly 'com.esotericsoftware.kryo:kryo:2.21'
    compile "org.apache.httpcomponents:httpclient:4.5.2"

    // XGBoost dependencies published into Maven central by H2O
    // Versioning rules may differ for XGBoost artifacts published by H2O
    compile("ai.h2o:xgboost4j_2.12:${xgboost4jVersion}") {
        exclude group: 'org.scala-lang', module: 'scala-compiler'
        exclude group: 'org.scala-lang', module: 'scala-reflect'
        exclude group: 'org.scala-lang', module: 'scala-library'
        exclude group: 'com.typesafe.akka', module: 'akka-actor_2.11'
        exclude group: 'com.esotericsoftware.kryo', module: 'kryo'
    }
    compile "ai.h2o:xgboost4j-linux-gpuv4:${xgboost4jVersion}"
    compile "ai.h2o:xgboost4j-linux-minimal:${xgboost4jVersion}"
    compile "ai.h2o:xgboost4j-osx-minimal:${xgboost4jVersion}"

    testCompile "junit:junit:${junitVersion}"
    testCompile "com.github.stefanbirkner:system-rules:1.18.0"
    testCompile 'com.esotericsoftware.kryo:kryo:2.21'
    testCompile project(path: ":h2o-core", configuration: "testArchives")
    testCompile project(path: ":h2o-algos", configuration: "testArchives")
    testRuntimeOnly project(":${defaultWebserverModule}")
}

apply from: "${rootDir}/gradle/dataCheck.gradle"

test {
    dependsOn ":h2o-core:testJar"
    // Note: multi node tests are ignored right now!
    dependsOn smalldataCheck, cpLibs, jar, testJar, testSingleNode, testMultiNode

    // Defeat task 'test' by running no tests.
    exclude '**'
}
