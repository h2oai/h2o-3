//
// H2O Web Module
//
import org.apache.tools.ant.taskdefs.condition.Os

description = "H2O web client"

// Represents external dependencies which will be put into web root and accessible
// via REST API
configurations {
    publishedJars
}

dependencies {
    // Dependency on shared model support which is served by web backend as a file
    publishedJars project(path: ':h2o-assemblies:genmodel', configuration: 'shadow')
}

static def checkPrerequisite(name) {
  def prefix = Os.isFamily(Os.FAMILY_WINDOWS) ? 'cmd /c ' : '/usr/bin/env '
  def command = prefix + name + ' -v'
  def proc
  try {
    proc = command.execute()
    if (proc != null) {
      proc.waitFor()
    }
  } catch(ignored) {
    return -1
  }
  if (proc == null || (proc != null && proc.exitValue() != 0)) {
    return -1
  }
  return 0
}

static def getCoffeePath() {
    '.' + File.separator + 'node_modules' + File.separator + '.bin' + File.separator + 'coffee'
}

task checkClientPrerequisites {
    doLast {
        def nodeInstallationInstruction = """\

To install node.js, try one of these:

Mac OS:
Use the official installer at http://nodejs.org/download/
OR
If you have homebrew installed, run 'brew install node'.

Linux:
See https://github.com/joyent/node/wiki/Installing-Node.js-via-package-manager

Windows:
Use the official installer at http://nodejs.org/download/

"""

        def nodeFailureMessage = "Could not detect a node.js installation on this system.\nInstall node.js and try again.\n" + nodeInstallationInstruction

        if (checkPrerequisite('node') != 0) {
            if (Os.isFamily(Os.FAMILY_MAC)) {
                if (checkPrerequisite('/usr/local/bin/node') != 0) {
                    throw new StopExecutionException(nodeFailureMessage)
                }
            } else {
                throw new StopExecutionException(nodeFailureMessage)
            }
        }
    }
}

task installNpmPackages(type: Exec) {
  commandLine = getOsSpecificCommandLine([ 'npm', 'install' ])
}

task installBowerPackages(type: Exec) {
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    commandLine 'cmd', '/c', 'node_modules\\.bin\\bower', 'install'
  } else {
    if (project.hasProperty("h2o.web.allow.root")) {
        commandLine 'node_modules/.bin/bower', 'install', '--allow-root'
    } else {
        commandLine 'node_modules/.bin/bower', 'install'
    }
  }
}

task copyFlowToWebRoot(type: Copy) {
  from 'lib/h2o-flow/build'
  into 'src/main/resources/www/flow'
}

task copyGenModelJarToWebRoot(type: Copy) {
  from {
      configurations.publishedJars.filter { it.name.contains("genmodel") }
  }
  into "src/main/resources/www/${h2oRESTApiVersion}/"
  rename { filename -> "h2o-genmodel.jar"}
}

task deleteBowerModules(type: Delete) {
  delete 'lib'
}

task deleteNodeModules(type: Delete) {
  delete 'node_modules'
}

task deleteFlowFromWebRoot(type: Delete) {
  delete 'src/main/resources/www/flow'
}

task deleteGenModelJarFromWebRoot(type: Delete) {
    delete "src/main/resources/www/${h2oRESTApiVersion}/h2o-genmodel.jar"
}


def exampleFlowsOutputDir = "$rootDir/h2o-web/src/main/resources/www/flow/packs"

task copyExampleFlows(type: Copy) {
  from "$rootDir/h2o-docs/src/product/flow/packs"
  into exampleFlowsOutputDir
}

task compileHelpFiles(type: Exec) {
  commandLine = getOsSpecificCommandLine([
          getCoffeePath(), 'make-help.coffee', 
          "$rootDir/h2o-docs/src/product/flow/README.md", 
          "$rootDir/h2o-web/src/main/resources/www/flow/help" 
  ])
}

task copyHelpImages(type: Copy) {
  from "$rootDir/h2o-docs/src/product/flow/images"
  into "$rootDir/h2o-web/src/main/resources/www/flow/help/images"
}

task cleanUpSmokeTest {
    doLast {
        new File(["results"].join(File.separator)).deleteDir()
    }
}

task compileAndInstallDocFiles(type: Exec) {
    doFirst {
        file("${buildDir}/tmp").mkdirs()
        standardOutput = new FileOutputStream("${buildDir}/tmp/h2o-web_compileAndInstallDocFiles.out")
    }
    H2OBuildVersion bv = new H2OBuildVersion(rootDir, version)
    def projectVersion = bv.getProjectVersion()
    def branchName = bv.getBranch()
    def buildNumber = bv.getBuildNumber()
    def lastCommitHash = bv.getLastCommitHash()
    commandLine = getOsSpecificCommandLine([
             getCoffeePath(), 'rest-api-reference.coffee',
             "--project_version=" + projectVersion,
             "--branch_name=" + branchName,
             "--build_number=" + buildNumber,
             "--last_commit_hash=" + lastCommitHash
    ])
}

task deleteDocFiles(type: Delete) {
    delete '../h2o-docs/web'
}

installNpmPackages.dependsOn checkClientPrerequisites
installBowerPackages.dependsOn installNpmPackages
copyFlowToWebRoot.dependsOn installBowerPackages

compileHelpFiles.dependsOn installNpmPackages

// Prepare resource stage fetch all necessary artifacts and put there under src/../resources
processResources.dependsOn copyFlowToWebRoot
processResources.dependsOn copyExampleFlows
processResources.dependsOn compileHelpFiles
processResources.dependsOn copyHelpImages
processResources.dependsOn copyGenModelJarToWebRoot

copyGenModelJarToWebRoot.dependsOn ":h2o-assemblies:genmodel:copyJar"

compileAndInstallDocFiles.dependsOn installNpmPackages
compileAndInstallDocFiles.dependsOn ":h2o-bindings:runGenerateRESTAPIBindingsSrc"
build.dependsOn compileAndInstallDocFiles

// Hook clean stage
clean.dependsOn deleteFlowFromWebRoot, cleanUpSmokeTest, deleteGenModelJarFromWebRoot, deleteDocFiles
