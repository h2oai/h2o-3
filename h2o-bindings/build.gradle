description = "H2O REST API bindings"

// Common constants
def runner = new File("$rootDir/scripts/run.py").canonicalPath
def binDir = file("bin")
def srcGenBindingsDir = file("src-gen/main") // TODO: this setting is not properly passed to gen_all.py

// Configuration for code generator
configurations {
  srcGenCompile
}
ext{
    pythonexe = findProperty("pythonExec") ?: "python"
    pipexe = findProperty("pipExec") ?: "pip"
    if (System.env.VIRTUAL_ENV) {
        pythonexe = "${System.env.VIRTUAL_ENV}/bin/python".toString()
        pipexe = "${System.env.VIRTUAL_ENV}/bin/pip".toString()
    }
}
dependencies {
    api 'com.squareup.retrofit2:retrofit:2.8.1'
    api 'com.squareup.retrofit2:converter-gson:2.8.1'
    api 'com.squareup.okhttp3:okhttp:4.6.0'
    api 'com.squareup.okio:okio:2.6.0'

    testImplementation project(":h2o-app")
    testImplementation project(":h2o-web")
    testImplementation project(":h2o-avro-parser")
    testImplementation "junit:junit:${junitVersion}"

    // Generator dependencies
    srcGenCompile project(path: ":h2o-assemblies:main", configuration: "shadow")
}

sourceSets {
  main {
    java {
      srcDir "$srcGenBindingsDir/java"
    }
  }
}

jar {
  manifest {
    attributes 'Main-Class': 'water.bindings.examples.retrofit.GBM_Example'
  }
}

test {
    maxParallelForks = 1
    maxHeapSize = "4g"
    forkEvery = 1
}

task runGenerateRESTAPIBindingsSrc(type: Exec) {
    workingDir binDir
    // Note: by default the generated sources get written to ./h2o-bindings/src-gen/*
    environment "H2O_JARFILE", configurations.srcGenCompile.getSingleFile()
    environment "H2O_JAVA_VERSION", JavaVersion.current()
    commandLine getOsSpecificCommandLine([pythonexe, "gen_all.py"])
}

task cleanGenerateRESTAPIBindings(type: Delete) {
    delete "$binDir/results"
    delete "$binDir/sandbox"
    delete "$srcGenBindingsDir"
}

runGenerateRESTAPIBindingsSrc.dependsOn configurations.srcGenCompile

compileJava.dependsOn runGenerateRESTAPIBindingsSrc
clean.dependsOn cleanGenerateRESTAPIBindings

//
// Setup Gradle application runner
//
apply plugin: 'application'
mainClassName = 'water.bindings.examples.retrofit.GBM_Example'
