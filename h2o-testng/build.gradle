description = "H2O Test Integration - TestNG"

dependencies {
	compile 'net.sf.opencsv:opencsv:2.3'
	compile 'mysql:mysql-connector-java:5.1.16'
    testCompile 'org.testng:testng:6.8'
    compile project(":h2o-core")
    compile project(":h2o-algos")
    testCompile project(path: ":h2o-core", configuration: "testArchives")
    testCompile project(path: ":h2o-algos", configuration: "testArchives")
}

test {
    def singleNode = rootProject.hasProperty("testNGSingleNode")
    def multipleNode = rootProject.hasProperty("testNGMultiNode")
    enabled = singleNode || multipleNode

    if (multipleNode) { jvmArgs('-ea',"-Xmx${testNGXmx}", "-Xms${testNGXmx}") }

    useTestNG{
        useDefaultListeners = true
        
        if(rootProject.hasProperty("testcaseId")){
        	systemProperty 'testcaseId', "$testcaseId"
        	println("algorithm and size parameter will be ignored.");
        }
        else{
	        if(rootProject.hasProperty("algorithm")){
	        	systemProperty 'algorithm', "$algorithm"
	        }
	        if(rootProject.hasProperty("size")){
	        	systemProperty 'size', "$size"
	        }
	    }
	    if(rootProject.hasProperty("dbConfigFilePath")){
	    	systemProperty 'dbConfigFilePath', "$dbConfigFilePath"
	    }
        
        if (singleNode) { suites("src/test/resources/singleNode.xml") }
        else { suites("src/test/resources/multipleNode.xml") }
    }
    
    testLogging.setShowStandardStreams(true)

    reports {
        html.enabled = false
        junitXml.outputPerTestCase = true
    }
}

task updateTestNGReport(type: Exec) {
    commandLine 'python', 'scripts/update_testng_report.py', '-d', 'build/reports/tests/'
}

test.finalizedBy(updateTestNGReport)
