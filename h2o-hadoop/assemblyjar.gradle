apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

description = 'H2O HDFS client shadowjar for Hadoop ' + hadoopVersion

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
  if (project.hasProperty('notYarn')) {
    compile('org.apache.hadoop:hadoop-core:' + hadoopMavenArtifactVersion)
  }
  else {
    compile('org.apache.hadoop:hadoop-client:' + hadoopMavenArtifactVersion)
  }
  compile project(':h2o-hadoop:h2o-' + hadoopVersion)
  compile project(':h2o-app')
  compile (project(':h2o-persist-hdfs')) {
    transitive = false
  }

  // For standalone mode to work with MapR, this extra library needs to be
  // included, and it's not pulled in by the dependency stuff;  this must
  // be a bug in MapR's packaging process.
  if (project.hasProperty("maprExtraDependency")) {
    compile(project.property("maprExtraDependency"))
  }
}


def hadoopShadowJarExcludes = ['META-INF/*.DSA',
                            'META-INF/*.SF',
                            'synchronize.properties',
                            'uploader.properties',
                            'test.properties',
                            'cockpitlite.properties',
                            'devpay_products.properties']

shadowJar {
  mergeServiceFiles()
  // CDH 5.3.0 provides joda-time v1.6 which is too old, shadow the library instead
  relocate 'org.joda.time', 'ai.h2o.org.joda.time'
  exclude hadoopShadowJarExcludes
  baseName = 'h2odriver'
  classifier = ''
  manifest {
    attributes 'Main-Class': 'water.hadoop.h2odriver'
  }
}

artifacts {
  archives shadowJar
}

jar.finalizedBy shadowJar
