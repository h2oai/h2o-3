description = "H2O Python Package"

dependencies {
    compile project(path: ":h2o-assemblies:main", configuration: 'shadow')
}

def buildVersion = new H2OBuildVersion(rootDir, version)

ext {
    PROJECT_VERSION = buildVersion.getProjectVersion()
    pythonexe = findProperty("pythonExec") ?: "python"
    pipexe = findProperty("pipExec") ?: "pip"
    if (System.env.VIRTUAL_ENV) {
        pythonexe = "${System.env.VIRTUAL_ENV}/bin/python".toString()
        pipexe = "${System.env.VIRTUAL_ENV}/bin/pip".toString()
    }
    testsPath = file("tests")
}

//
// Create a file with version for Python dist task
//
task createVersionFiles() {
    doLast {
        file("${buildDir}/h2o").mkdirs()
        File version_file = new File("${buildDir}/h2o", "version.txt")
        version_file.write(PROJECT_VERSION)

        File build_file = new File("${buildDir}/h2o", "buildinfo.txt")
        build_file.write(buildVersion.toString())
    }
}

task verifyDependencies(type: Exec) {
    commandLine getOsSpecificCommandLine([
            pythonexe, "scripts/verify_requirements.py",
            "--metayaml", "conda/h2o/meta.yaml",
            "--kind", "build"
    ])
}

task verifyTestDependencies(type: Exec) {
    commandLine getOsSpecificCommandLine([
            pythonexe, "scripts/verify_requirements.py",
            "--metayaml", "conda/h2o/meta.yaml",
            "--kind", "test"
    ])
}

task copyH2OJar(type: Copy, dependsOn: [configurations.compile]) {
    from "${rootDir}/build/h2o.jar"
    into "${buildDir}/h2o/backend/bin/"
}

task copySrcFiles(type: Copy, dependsOn: [copyH2OJar]) {
    from ("${projectDir}") {
        include "setup.py"
        include "setup.cfg"
        include "h2o/**"
        include "DESCRIPTION.rst"
        include "MANIFEST.in"
        include "README.md"
        include "requirements.txt"
    }
    into "${buildDir}"
}

task buildDist(type: Exec, dependsOn: [verifyDependencies, createVersionFiles, copySrcFiles]) {
    workingDir buildDir
    doFirst {
        file("${buildDir}/tmp").mkdirs()
        standardOutput = new FileOutputStream(file("${buildDir}/tmp/h2o-py_buildDist.out"))
    }
    commandLine getOsSpecificCommandLine([pythonexe, "setup.py", "bdist_wheel"])
}

task smokeTest(type: Exec, dependsOn: [verifyTestDependencies]) {
    workingDir testsPath
    List<String> args = [pythonexe, '../../scripts/run.py', '--wipeall', '--testsize', 's']
    if (project.hasProperty("jacocoCoverage")) {
        args << '--jacoco'
    }
    commandLine getOsSpecificCommandLine(args)
}

task pythonVersion(type: Exec) {
    doFirst {
        println(System.env.VIRTUAL_ENV)
        println(environment)
    }
    commandLine getOsSpecificCommandLine([pythonexe, "--version"])
}


task cleanUpSmokeTest(type: Delete) {
    delete file("${testsPath}/results")
}

task cleanCoverageData(type: Delete) {
    delete file("${testsPath}/results/jacoco")
}

task cleanBuild(type: Delete) {
    doFirst {
        println "Cleaning..."
    }
    delete file("build/")
    delete fileTree(dir: "$projectDir/h2o" , include: '**/*.pyc')
}

//
// Define the dependencies
//
clean.dependsOn cleanBuild, cleanUpSmokeTest, cleanCoverageData
build.dependsOn buildDist
