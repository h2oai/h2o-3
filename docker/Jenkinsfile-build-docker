@Library('test-shared-library') _
import ai.h2o.ci.buildsummary.StagesSummary

DEFAULT_LABEL = 'mr-0xc10'

// initialize build summary
buildSummary('https://github.com/h2oai/h2o-3', true)
// use default StagesSummary implementation
buildSummary.get().addStagesSummary(this, new StagesSummary())

properties([
    parameters([
        booleanParam(defaultValue: false, description: 'Whether to build GPU enabled images', name: 'useGPU'),
        booleanParam(defaultValue: false, description: 'Build even if the image is already present in the repository', name: 'forceBuild'),
        string(defaultValue: 'master', description: 'H2O-3 Branch used to load dependencies.', name: 'h2oBranch')
    ])
])

IMAGE_NAME_PREFIX = 'docker.h2o.ai/opsh2oai/h2o-3'
GPU_SUFFIX = params.useGpu ? '-gpu' : ''

UBUNTU_BASE_IMAGE = 'ubuntu:16.04'
GPU_BASE_IMAGE = 'nvidia/cuda:9.0-cudnn7-devel-ubuntu16.04'

JDK_VERSIONS = ['7', '8', '10', '11']
PYTHON_VERSIONS = ['2.7', '3.5', '3.6']
R_VERSION = ['3.4.1', '3.3.3']

def pipelineContext

ansiColor('xterm') {
    timestamps {
        def checkoutStageName = 'Checkout and Init'
        buildSummary.stageWithSummary(checkoutStageName) {
            node (DEFAULT_LABEL) {
                buildSummary.refreshStage(checkoutStageName)
                cleanWs()
                def scmEnv = checkout scm

                def final pipelineContextFactory = load('scripts/jenkins/groovy/pipelineContext.groovy')
                pipelineContext = pipelineContextFactory('.', 'MODE_BUILD_DOCKER', scmEnv, true)
                def currentVersion = pipelineContext.getBuildConfig().getDefaultImageVersion()
                currentBuild.displayName = "v${currentVersion} #${currentBuild.number}"

                if (!params.forceBuild) {
                    if (pipelineContext.getUtils().dockerImageExistsInRegistry(this, 'docker.h2o.ai', 'opsh2oai/h2o-3/dev-base', "${currentVersion}")) {
                        error "Image already exists. Use forceBuild if you wish to overwrite."
                    }
                } else {
                    manager.addBadge("warning.gif", "forceBuild enabled")
                }

                dir('docker') {
                    withCredentials([[$class: 'AmazonWebServicesCredentialsBinding', accessKeyVariable: 'AWS_ACCESS_KEY_ID', credentialsId: 'AWS S3 Credentials', secretKeyVariable: 'AWS_SECRET_ACCESS_KEY']]) {
                        docker.image('docker.h2o.ai/s3cmd').inside("-e AWS_ACCESS_KEY_ID=${AWS_ACCESS_KEY_ID} -e AWS_SECRET_ACCESS_KEY=${AWS_SECRET_ACCESS_KEY}") {
                            sh "s3cmd get s3://artifacts.h2o.ai/releases/oracle/jdk-7/x64-linux/jdk1.7.0_80.zip"
                            sh "s3cmd get s3://artifacts.h2o.ai/releases/oracle/jdk-10/x64-linux/jdk-10.0.2_linux-x64_bin.tar.gz"
                        }
                    }
                    stash name: 'docker-sources'
                }
            }
        }

        buildInParallel(pipelineContext, ['dev-base'])
        buildInParallel(pipelineContext, ['dev-jdk-8-base', 'dev-jdk-others-base'])
        buildInParallel(pipelineContext, ['dev-python-base', 'dev-r-base'])

        def componentImages = []
        PYTHON_VERSIONS.each {version ->
            componentImages += "dev-python-${version}"
        }
        R_VERSION.each {version ->
            componentImages += "dev-r-${version}"
        }
        JDK_VERSIONS.each {version ->
            componentImages += "dev-jdk-${version}"
        }
        buildInParallel(pipelineContext, componentImages)

        buildInParallel(pipelineContext, ["dev-build-base"])
        buildInParallel(pipelineContext, ["dev-build-gradle-${pipelineContext.getBuildConfig().getCurrentGradleVersion()}"])
        buildInParallel(
            pipelineContext,
            [
                "dev-build-hadoop-gradle-${pipelineContext.getBuildConfig().getCurrentGradleVersion()}"
            ]
        )
        buildInParallel(pipelineContext, ["dev-release-gradle-${pipelineContext.getBuildConfig().getCurrentGradleVersion()}"])

        def componentJdkImages = []
        PYTHON_VERSIONS.each {version ->
            JDK_VERSIONS.each {jdkVersion ->
                componentJdkImages += "dev-python-${version}-jdk-${jdkVersion}"
            }
        }
        R_VERSION.each {version ->
            JDK_VERSIONS.each {jdkVersion ->
                componentJdkImages += "dev-r-${version}-jdk-${jdkVersion}"
            }
        }
        buildInParallel(pipelineContext, componentJdkImages)
    }
}

private buildInParallel(final pipelineContext, final targets) {
    parallel(targets.collectEntries { target ->
        [
            target, {
                def buildImageStageName = "Build ${target}"
                buildSummary.stageWithSummary(buildImageStageName) {
                    node (DEFAULT_LABEL) {
                        buildSummary.refreshStage(buildImageStageName)
                        dir (target) {
                            deleteDir()
                            unstash 'docker-sources'
                            pullPreviousVersionIfRequired(pipelineContext, target)
                            sh """
                                make \
                                    H2O_BRANCH=${params.h2oBranch} \
                                    VERSION=${pipelineContext.getBuildConfig().getDefaultImageVersion()} \
                                    GPU=${params.useGPU} \
                                    PUSH=true \
                                    ${target}
                            """
                        }
                    }
                }
            }
        ]
    })
}

def pullPreviousVersionIfRequired(final pipelineContext, final String image) {
    def previousVersion = pipelineContext.getBuildConfig().getDefaultImageVersion() - 1
    // hack for first version of image
    if (previousVersion <= 0) {
        previousVersion = 1
    }
    if (pipelineContext.getUtils().dockerImageExistsInRegistry(this, 'docker.h2o.ai', "opsh2oai/h2o-3/${image}", "${previousVersion}")) {
        retryWithDelay(3 /* retries */, 120 /* delay in sec */) {
            sh "docker pull ${IMAGE_NAME_PREFIX}/${image}:${previousVersion}"
        }
    }
}
