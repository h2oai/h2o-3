apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java-library'

description = "H2O Application Assembly for Secure deployments using H2O Steam"

apply from: '../standalone_assembly.gradle'

configurations {
    api.exclude module: "h2o-jaas-pam"
}

// Dependencies
dependencies {
    api(project(":h2o-app")) {
        exclude module: "${defaultWebserverModule}"
    }
    runtimeOnly project(":h2o-jetty-9-minimal")
    api project(":h2o-logging-impl-log4j2")
    api project(":h2o-web")
    api project(":h2o-persist-gcs")
    api(project(":h2o-persist-s3")) {
        exclude group: "org.apache.hadoop"
    }
    api project(":h2o-persist-http")
    api(project(":h2o-persist-hdfs")) {
        exclude group: "org.apache.hadoop"
    }
    api(project(":h2o-parquet-parser")) {
        exclude group: "org.apache.hadoop"
    }
    api "org.apache.hadoop:hadoop-hdfs-client:3.3.2"
    api("org.apache.hadoop:hadoop-common:3.3.2") {
        exclude group: "com.sun.jersey"
        exclude group: "javax.servlet"
        exclude group: "org.apache.avro"
        exclude group: "org.apache.curator"
        exclude group: "org.apache.zookeeper"
        exclude group: "org.eclipse.jetty"
    }
    api("org.apache.hadoop:hadoop-aws:3.3.2") {
        exclude group: "com.amazonaws", module: "aws-java-sdk-bundle"
    }
    api project(":h2o-k8s-int")
}

shadowJar {
  mergeServiceFiles()
  classifier = ''
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/*.SF'
  exclude 'synchronize.properties'
  exclude 'uploader.properties'
  exclude 'test.properties'
  exclude 'cockpitlite.properties'
  exclude 'devpay_products.properties'
  manifest {
    attributes 'Main-Class': 'water.H2OApp'
    attributes 'Add-Opens': 'java.base/java.lang java.base/java.util java.base/java.lang.reflect'
    attributes 'Multi-Release': 'true'
  }
  transform(com.github.jengelman.gradle.plugins.shadow.transformers.IncludeResourceTransformer.class) {
    file = file("${buildDir}/reports/license/dependency-license.xml")
    resource = "META-INF/license/h2o-depenencies-licenses.xml"
  }
  transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}
