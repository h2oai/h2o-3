apply plugin: 'java'

//
// H2O Core Module
//
description = "H2O Core"

dependencies {
  // Dependency on shared model support
  compile project (':h2o-genmodel')
  // Required for h2o-core
  compile "joda-time:joda-time:2.9.9"
  compile "gov.nist.math:jama:1.0.3"
  compile 'org.javassist:javassist:3.23.1-GA'
  compile "org.apache.commons:commons-math3:3.3"
  compile "commons-codec:commons-codec:1.9"
  compile "commons-io:commons-io:2.4"
  compile "org.eclipse.jetty.aggregate:jetty-servlet:8.1.17.v20150415"
  compile "org.eclipse.jetty:jetty-plus:8.1.17.v20150415"
  compile ("com.github.rwl:jtransforms:2.4.0") { exclude module: "junit" }
  compile project(":h2o-jaas-pam")

  compile("log4j:log4j:1.2.17") {
    exclude module: "activation" 
    exclude module: "jms" 
    exclude module: "jmxri" 
    exclude module: "jmxtools" 
    exclude module: "mail" 
  }

  // guava only needed by hadoop now, not by H2O
  compile 'com.google.guava:guava:16.0.1'
  compile "com.google.code.gson:gson:2.6.2"
  compile 'commons-lang:commons-lang:2.6'
  
  // Duke library: collection of String comparators
  compile ('no.priv.garshol.duke:duke:1.2'){
    exclude group: 'org.apache.lucene', module: 'lucene-core'
    exclude group: 'org.apache.lucene', module: 'lucene-analyzers-common'
    exclude group: 'org.apache.lucene', module: 'lucene-spatial'
    exclude group: 'org.mapdb', module: 'mapdb'
  }

  // Jets3t is required by S3N support
  // compile 'net.java.dev.jets3t:jets3t:0.6.1'

  testCompile "junit:junit:${junitVersion}"
  testCompile "org.mockito:mockito-core:2.18.0"
  testCompile "org.apache.httpcomponents:httpclient:4.1"
  testCompile "com.github.stefanbirkner:system-rules:1.18.0"
  testCompile "org.apache.derby:derby:10.14.2.0"
}

apply from: "${rootDir}/gradle/dataCheck.gradle"

javadoc {
  exclude "**/fvec/C*Chunk.java/**"
  exclude "**/nbhm/**"
}

jar {
  manifest {
    attributes 'Main-Class': 'water.H2O'
  }
}

// The default 'test' behavior is broken in that it does not grok clusters.
// For H2O, all tests need to be run on a cluster, where each JVM is
// "free-running" - it's stdout/stderr are NOT hooked by another process.  If
// they are hooked (e.g., by the gradle driver process) then the stdout/err get
// buffered and when all CPUs are maxed out (happens over a large fraction of
// the test run) no output is visible.  If the JVMs then crash (again, common
// enough), we get NO output for the test run.  So instead we need to arrange a
// complete cluster of free-running JVMs and redirect all output (at the OS
// level) to files - then scrape the files later for test results.
test {
  dependsOn smalldataCheck, cpLibs, testMultiNode, testJar

  // Defeat task 'test' by running no tests.
  exclude '**'
}

def buildVersionFile = new File(projectDir, "/src/main/java/water/init/BuildVersion.java")

task generateBuildVersionJava {
  doLast {
    H2OBuildVersion bv = new H2OBuildVersion(rootDir, version)
    bv.emitBuildVersionJavaFileIfNecessary(buildVersionFile)
  }
}

compileJava.dependsOn generateBuildVersionJava

task cleanBuildVersionJava(type: Delete) {
    delete buildVersionFile
}

clean.dependsOn cleanBuildVersionJava

apply from: '../gradle/javaIgnoreSymbolFile.gradle'
