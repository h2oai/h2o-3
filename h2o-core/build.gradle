description = "H2O Core"

apply from: '../ci/java.gradle'

mainClassName = "water.H2O"

dependencies {
  // Required for h2o-core
  compile "joda-time:joda-time:2.3"

  compile("log4j:log4j:1.2.15") { 
    exclude module: "activation" 
    exclude module: "jms" 
    exclude module: "jmxri" 
    exclude module: "jmxtools" 
    exclude module: "mail" 
  }

  // WARNING: turning back on transitive dependencies brings in 19M in 37 jars... 
  // All unneeded to read from HDFS.
  compile("org.apache.hadoop:hadoop-common:2.3.0") { 
    transitive = false
  }
  compile "com.google.guava:guava:16.0.1"

  compile "commons-logging:commons-logging:1.1.1"
  compile "commons-collections:commons-collections:3.0"
  compile "org.javassist:javassist:3.15.0-GA"

  testCompile "junit:junit:4.+"
  testCompile "org.testng:testng:6.8"
}

sourceSets {
  test {
    java {
      // Slam in a new fully specified list of directories.
      // We don't want to pick up any of the default directories.
      // Specifically, src/test has a bunch of junit stuff that doesn't run
      // properly from gradle or the IDE.
      ArrayList<String> arr = new ArrayList<String>()
      arr.add('src/ng/java')
      setSrcDirs(arr)
    }
  }
}

test {
  useTestNG() {
    suites new File(projectDir, 'src/ng/resources/testng-multinode.xml')
    suites new File(projectDir, 'src/ng/resources/testng.xml')
  }
}

javadoc {
  exclude "**/fvec/C*Chunk.java/**"
  exclude "**/nbhm/**"
}

jar {
  manifest {
    attributes 'Main-Class': 'water.H2O'
  }
}

task testMultiNode(type: Exec) {
  commandLine './testMultiNode'
}

// Cliff command-line copy libs.  Not required for standard gradle build.
task cpLibs(type: Copy) {
  from configurations.compile.files 
  from configurations.testCompile.files 
  into '../lib'
}

apply from: '../ci/javaIgnoreSymbolFile.gradle'
